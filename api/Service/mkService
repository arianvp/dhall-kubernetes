-- Prelude
   let Prelude = ../../Prelude.dhall
in let kv = Prelude.JSON.keyText

-- Kubernetes types and defaults
in let Types   = ./RawTypes
in let default = ./RawDefaults

-- Types for dynamic records
in let KV     = { mapKey : Text, mapValue : Text }
in let ListKV = List KV


in let mkService : ./Service → Types.Service =

  λ(service : ./Service) →

     let selector = Some [kv "app" service.name]

  in let meta = default.meta
  { name = service.name } //
  { labels = selector
  , annotations = Some service.annotations
  }

  -- Handlers for the ServiceType union
  in let handlers =
    { ClusterIP    = \(_ : {}) -> "ClusterIP"
    , NodePort     = \(_ : {}) -> "NodePort"
    , LoadBalancer = \(_ : {}) -> "LoadBalancer"
    , ExternalName = \(_ : {}) -> "ExternalName"
    }

  in let spec = default.spec //
  { type = Some (merge handlers service.type : Text)
  , ports = Some
    [ default.port
      { port = service.outPort } //
      { targetPort = Some (default.Int service.containerPort) }
    ]
  , selector = selector
  }

  in default.service
  { metadata = meta
  } //
  { spec = Some spec
  } : Types.Service

in mkService
